apply plugin: 'sonar-runner'

def env = project.hasProperty('env') ? project.getProperty('env') : 'test'
logger.quiet "Loading configuration for environment '$env'."
def configFile = file("$rootDir/gradle/config/buildConfig.groovy")
def parsedConfig = new ConfigSlurper(env).parse(configFile.toURL())

sonarRunner {
    toolVersion = '2.4'
    sonarProperties {
        property "sonar.host.url", "http://$System.env.SONAR_PORT_9000_TCP_ADDR:9000"
        property "sonar.jdbc.url", "jdbc:mysql://$System.env.SONAR_MYSQL_PORT_3306_TCP_ADDR/sonar?useUnicode=true&characterEncoding=utf8&rewriteBatchedStatements=true"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "123qwe"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

allprojects {
    apply plugin: 'maven-publish'
    apply from: "$rootDir/gradle/versioning.gradle"
    ext.config = parsedConfig
    ext.fullRepoUrl = "http://$System.env.ARTIFACTORY_PORT_8080_TCP_ADDR:8080/artifactory/$config.binaryRepository.name"

    group = 'io.mercer.handbags'

    repositories {
        jcenter()
        maven { url "http://nexus.broadleafcommerce.org/nexus/content/repositories/snapshots/" }
    }

    publishing {
        repositories {
            maven {
                url fullRepoUrl
                credentials {
                    username = config.binaryRepository.username
                    password = config.binaryRepository.password
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

task acceptance {
    logger.quiet "Running all automated acceptance tests ..."
}